Check 91CF0B84
Auto 8224
   1 CLEAR VAL "62991"
   4 REM : USR 32768
   5 REM : LEN h$\#1993,n$\#1999,m$\#19917,r$\#1991,i$\#1993,b$\#19918,w$\#1997
   6 REM : INT+ c,z,w,md,di
   7 REM : INT p
   9 REM : OPEN #
  10 POKE 63038,1: GO SUB 9e3: GO SUB 250
  13 GO SUB 100
  15 IF NOT p THEN GO TO 7450
  17 IF p=1 THEN GO TO 40
  20 IF p=2 THEN GO TO 2400
  25 IF p=3 THEN GO TO 2500
  30 GO TO 810
  40 POKE 63038,5: GO TO 7450
  60 LET z=di-520: LET c=INT (z/256): POKE 23606,z-c*256: POKE 23607,c: RETURN
  69 POKE 23606,0: POKE 23607,60: RETURN
  99 REM \: 128Menu
 100 PRINT AT 7,7; PAPER 7; INK 0; INVERSE 1;(w$+"       ")( TO 8);: GO SUB 60: PRINT b$+"\::": GO SUB 69
 130 FOR z=1 TO 5: PRINT PAPER 7; INK 0;AT z+7,7;" "+z$(z)+" "; BRIGHT 0; OVER 1; PAPER 1;" ": NEXT z: PRINT PAPER 7; INK 0;AT 13,7;CHR$ 23+CHR$ 21: PRINT PAPER 7;AT 13,21; OVER 1; BRIGHT 0; PAPER 1; INK 0;" ";AT 14,8;CHR$ 23+CHR$ 22: PLOT INK 0; PAPER 7;56,111: DRAW INK 0; PAPER 7;0,-47: DRAW INK 0; PAPER 7;111,0: DRAW INK 0; PAPER 7;0,47: LET p=0
 140 PRINT OVER 1; PAPER 5; INK 0;AT p+8,7;CHR$ 23+CHR$ 21: BEEP .009,-11
 150 PAUSE 0: LET c=PEEK 23560
 160 IF c= CODE "a" OR c=10 THEN GO SUB 190
 165 IF c= CODE "q" OR c=11 THEN GO SUB 210
 170 IF c=13 THEN RETURN
 180 GO TO 150
 190 GO SUB 230: LET p=p+1: IF p>4 THEN LET p=0
 200 GO TO 140
 210 GO SUB 230: LET p=p-1: IF p<0 THEN LET p=4
 220 GO TO 140
 230 PRINT OVER 1; PAPER 7; INK 0;AT p+8,7;CHR$ 23+CHR$ 21: RETURN
 250 BRIGHT 1: PRINT PAPER 7; INK 0;AT 21,0; INVERSE 1;w;"k MODE (";md;")";CHR$ 23+CHR$ 26: GO SUB 60: PRINT b$+"\::": GO SUB 69: PRINT #1;AT 1,4;">CharExp v0.96 Berta 2<"
 260 DIM z$(5,12): LET z$(1)="Start Help": LET z$(2)="Edit Help": LET z$(3)="Credits": LET z$(4)="Gratefulness": LET z$(5)="Exit": RETURN
 300 LET c=2: FOR z=0 TO 7: POKE di+z,c-1: POKE 23408+z,255-PEEK (di+z): LET c=c*2: NEXT z: RETURN
 809 REM \: ExitHelp
 810 POKE 63038,1: BEEP .09,12.9
 820 GO SUB 69: BRIGHT 0: PRINT #1;AT 1,0; PAPER 8; INK 9;">Use RUN to reload program": PAPER 7: BORDER 7: INK 0: PAUSE 0: STOP
2319 REM \: GUI_SUBR
2320 PAPER 0: BORDER 0: BRIGHT 1: CLS : PLOT 0,167: DRAW 8,0: DRAW INK 2;8,0: DRAW INK 6;8,0: DRAW INK 4;8,0: INK 5: DRAW 223,0: DRAW 0,-167: INK 2: DRAW -255,0: DRAW 0,166
2330 PRINT AT 0,31; BRIGHT 1; PAPER 2; INK 7;"X"
2340 GO SUB 60: PRINT #1; BRIGHT 1;AT 0,27;b$;AT 1,26;b$+"\::": PRINT AT 0,1;b$
2350 FOR z=1 TO 4: PRINT AT 22-z,z+27;b$( TO 19-3*z): NEXT z
2360 PRINT AT 0,0; BRIGHT 0; INK 5; PAPER 8;"\a": GO SUB 69: REM \: Icon_mano
2370 PRINT AT 2,1; BRIGHT 0; PAPER 6;CHR$ 23+CHR$ 31: RETURN
2390 PRINT #1;AT 0,0;CHR$ 23+CHR$ 26: RETURN
2399 REM \: Credits
2400 GO SUB 2320
2405 GO SUB 2390: PRINT AT 2,12; INK 0; PAPER 8; BRIGHT 0;"Credits"+CHR$ 23+CHR$ 31: GO TO 2490
2490 PRINT INK 4;AT 5,3;"This isn't implemented yet.";AT 7,2;"Sorry to be a lazy asshole ;)": GO TO 810
2499 REM \: Acknowledgments
2500 GO SUB 2320
2505 GO SUB 2390: PRINT AT 2,8; INK 0; PAPER 8; BRIGHT 0;"Acknowledgments";TAB 31: GO TO 2490
7220 GO SUB 7320: GO TO 2405
7230 GO SUB 7320: GO TO 2505
7240 FOR z=7 TO 15: PRINT AT z,7; PAPER 0;TAB 22: NEXT z: GO TO 7530
7250 PRINT AT 2,28; BRIGHT 0; PAPER 8; INK 3;p: BEEP .01,-10: RETURN
7260 LET p=5: IF PEEK 63038=5 THEN GO TO 7240
7265 GO TO 7525
7280 LET p=PEEK 63038: GO SUB 7250: GO TO 7240
7300 LET z$(1)="Back to Help": POKE 63038,p: GO SUB 100
7303 IF NOT p THEN GO TO 7280
7305 IF p=1 THEN GO TO 7260
7307 IF p=2 THEN GO TO 7220
7309 IF p=3 THEN GO TO 7230
7311 GO TO 810
7320 FOR z=3 TO 20: PRINT AT z,1;CHR$ 23+CHR$ ((18-z AND z>18)+31): NEXT z: RETURN : REM \ .\: CleanScr_SUBR
7400 POKE 63038,p: POKE VAL "23232",87: BEEP .03,0: GO TO 820: REM \ :Quitting
7449 REM \: HelpStart
7450 GO SUB 2320: INK 5: BEEP .01,-10: LET p=PEEK 63038
7460 LET n$="_________": LET h$=CHR$ 16+CHR$ 4+".": LET p=PEEK 63038: LET m$=CHR$ 16+CHR$ 6+"+"+CHR$ 16+CHR$ 1+CHR$ 17+CHR$ 7+"CShift"+CHR$ 17+CHR$ 0+CHR$ 16+CHR$ 4: LET i$=CHR$ 16+CHR$ 6+">": INK 6
7470 PRINT AT 2,13; PAPER 8; BRIGHT 0; INK 0;"Help"
7499 REM \: HelpScreen
7500 PRINT #1; PAPER 0;AT 1,0;" ";i$; INK 5; BRIGHT 1;"Char"; BRIGHT 0;"Exp "; INK 1;"v"; BRIGHT 1; INK 1;"0"; BRIGHT 0;"."; BRIGHT 1;"96"; BRIGHT 0; INK 3;" Berta 2"; INK 6; BRIGHT 1;"<"
7502 PRINT AT 2,23; BRIGHT 0; PAPER 8; INK 1;"Page "; INK 3;p; INK 1;"/"; INK 3;"5"
7503 PRINT AT 0,6; PAPER 1;" >2021-22, Saul Saborido ": POKE VAL "22528",13: POKE VAL "22529",74: POKE VAL "22533",105: PRINT #1;AT 0,0; PAPER 1; INK 7; BRIGHT 1;"Enter"; INK 6; PAPER 0;":"; INK 4;"Menu    Back  Next": PLOT PAPER 8;155,174: DRAW PAPER 8;1,1: GO TO 7530
7504 GO SUB 7560
7505 POKE VAL "23246",79: POKE VAL "23252",79: POKE 23658,0: PAUSE 0: LET c=PEEK 23560
7506 IF c= CODE "n" OR c= CODE "p" THEN GO TO 7520
7508 IF c= CODE "b" OR c= CODE "o" THEN GO TO 7523
7509 IF c=13 THEN GO TO 7300
7514 GO TO 7505
7520 LET p=p+1: POKE VAL "23252",87: IF p>5 THEN LET p=1
7521 GO TO 7525
7523 LET p=p-1: POKE VAL "23246",87: IF p<1 OR p>9 THEN LET p=5
7525 GO SUB 7250: GO SUB 7320
7530 IF p=1 THEN GO SUB 7560
7535 IF p=2 THEN GO SUB 7650
7540 IF p=3 THEN GO SUB 7700
7545 IF p=4 THEN GO SUB 7800
7547 IF p=5 THEN GO SUB 7750
7550 GO TO 7505
7559 REM \: HelpPage1_SUBR
7560 PRINT AT 4,1; BRIGHT 0; INK 7;n$; INVERSE 1;"Displacement"; INVERSE 0;n$
7563 PRINT AT 6,1;i$; INK 4;"O,P:"; INK 6;"-+"; INK 4;"1"; INK 1;" Char  ";i$; INK 4;"O,P:"; INK 6;"-+"; INK 4;"4 "; INK 1;"Chars ";AT 8,1;i$; INK 4;"Q,A:"; INK 6;"-+"; INK 4;"4"; INK 5;" Bytes ";i$; INK 4;"W,S:"; INK 6;"-+"; INK 4;"1 "; INK 5;"Byte"
7575 PRINT AT 10,1;i$; INK 4;"I,K:"; INK 6;"-+"; INK 4;"8"; INK 1;" Chars ";i$; INK 4;"I,K:"; INK 6;"-+"; INK 4;"96"; INK 1;" Chars";AT 11,20; BRIGHT 0; INK 4;"(1 "; INK 6;"CharSet"; INK 4;")"
7577 FOR z=22737 TO 22865 STEP 64: POKE z-15,79: POKE z-13,79: POKE z,121: POKE z+2,121: NEXT z
7580 PRINT AT 13,1;i$; INK 4;"W,S:"; INK 4;"Up"; INK 6;"/"; INK 4;"down "; INK 6; INVERSE 1;"selection": POKE VAL "22946",79: POKE VAL "22948",79
7585 PRINT AT 15,1; BRIGHT 0; INK 7;n$( TO 7); INVERSE 1;"Keys Highlighting"; INVERSE 0;n$( TO 6)
7590 PRINT AT 17,1;i$; PAPER 1; INK 9;"[]"; PAPER 0; INK 4;":Normal use ";i$; PAPER 7; INK 1;"[]"; PAPER 0; INK 4;":";m$;" use"
7595 PRINT AT 19,1;i$; PAPER 1; INK 6;"[]"; PAPER 0; INK 4;":"; INK 6;"Both "; INK 4;"uses  ";i$; PAPER 2; INK 9;"[]"; PAPER 0; INK 4;":Active": RETURN
7649 REM \ :\' HelpPage2_SUBR
7650 PRINT INK 7; BRIGHT 0;AT 4,1;n$; INVERSE 1;"General Keys"; INVERSE 0;n$
7665 PRINT AT 6,1;">MAddr"; INK 4;": Change "; INK 6;"memory address";h$;AT 8,1;i$; INK 5;"Val"; INK 4;": Change "; INK 6; INVERSE 1;"selected"; INVERSE 0; INK 5;" value";h$
7670 PRINT AT 10,1;i$; INK 4;"Binary: "; INK 6;"Shows "; INK 4;"zeros"; INK 6;" & "; INK 7;"ones";h$;AT 12,1;i$; INK 4;"Hex: Change to "; INK 1;"hexa"; INK 6;"/"; INK 5;"decimal";h$
7675 PRINT AT 14,1;i$; INK 4;"Grid: Change grid colour.";AT 16,1; PAPER 0;i$; INK 4;"Loc: "; INK 5;"Useful locations"; INK 4;" menu."
7680 PRINT AT 18,1;i$; INK 4;"Rld: "; INK 6;"Refresh "; INK 4;"grid ";m$;" to";AT 19,5; INK 2;"reload"; INK 4;" this program."
7681 FOR z=1 TO 6: POKE z+22995,(z>2)+z+64: NEXT z
7683 FOR z=22722 TO 23106 STEP 64: POKE z,79-(z=23106)+(z=22914)*42: NEXT z: RETURN
7699 REM \ :\' HelpPage3_SUBR
7700 PRINT AT 4,1;i$; INK 5;"Zr0"; INK 4;": "; INK 6; INVERSE 1;"Selected"; INVERSE 0; INK 5;" value"; INK 4;" to "; INK 7;"Zer0";h$;AT 6,1;i$; INK 4;"MHB"; INK 4;": "; INK 0; PAPER 2;"MICRO"; INK 7;"HOBBY"; PAPER 0; INK 6;" CharSet"; INK 4;" sel."
7703 PRINT AT 8,1;i$; INK 1;"Del"; INK 4;": "; INK 2;"Delete"; INK 1;" character";h$;AT 10,1;i$; INK 4;"Edit"; INK 6;","; PAPER 1; INK 7;"Enter"; PAPER 0; INK 4;":"; INK 6;" Edit "; INK 4;"mode."
7711 PRINT AT 12,1;i$; INK 4;"INv: ";: LET r$="T": GO SUB 7790
7716 FOR z=22658 TO 22914 STEP 64: POKE z+(z<22724)*2+(z=22914),121-(z=22850 OR z=22978 OR z=22914)*42: NEXT z: RETURN
7740 RETURN
7749 REM \: EditHelpPage5_SUBR
7750 PRINT AT 4,1; BRIGHT 0; INK 7;n$+"_"; INVERSE 1;"Edit Mode"; INVERSE 0;n$+"__"
7753 PRINT AT 6,9;i$; PAPER 1; INK 7;"Space"; PAPER 0; INK 4;": "; INK 5;"ON"; INK 6;"/"; INK 2;"OFF "; INK 3;"pixel";h$;AT 8,9;i$; INK 4;"N: "; INK 3;"Px. "; INK 5;"ON";AT 8,20;i$; INK 4;"M: "; INK 3;"Px. "; INK 2;"OFF": POKE VAL "22794",79: POKE VAL "22805",79
7755 PRINT AT 10,9;i$; INK 4;"O,P: "; INK 6;"-+"; INK 4;"1 "; INK 1;"Char"; INK 4;" & "; INK 6;"save";h$;AT 9,2; INK 1; BRIGHT 0;"O";AT 9,6;"P": POKE VAL "22858",121: POKE VAL "22860",121
7760 PRINT AT 7,2; INK 4;"\\ ^ /";AT 9,1;"<";AT 9,7;">";AT 11,2;"/   \\";AT 8,3; PAPER 1; INK 7; BRIGHT 0;"Q"; BRIGHT 1;"W"; BRIGHT 0;"E";AT 9,3; BRIGHT 1;"A"; BRIGHT 0;"S"; BRIGHT 1;"D";AT 10,3; BRIGHT 0;"Z|C": POKE VAL "22852",68
7765 INK 4: PLOT 12,75+24: DRAW 3,0: PLOT 56,75+24: DRAW 4,0: PLOT 34,67+24: DRAW 2,-2: DRAW 2,2: PLOT 17,90+24: DRAW 0,3: DRAW 3,0: PLOT 51,93+24: DRAW 3,0: DRAW 0,-3: PLOT 50,57+24: DRAW 3,0: DRAW 0,3: PLOT 18,60+24: DRAW 0,-3: DRAW 3,0: INK 6
7770 PRINT AT 20,1;">"; INK 4;"Bkg: "; INK 5;"ON"; INK 6;"/"; INK 2;"OFF "; INK 4;"Seamless mode.";AT 12,9;i$; INK 4;"Inv: ";: POKE VAL "23170",121: LET r$="R": GO SUB 7790: POKE VAL "22922",79: RETURN
7789 REM \: Shared_SUBR
7790 PRINT INVERSE 1;"Inverse"; INVERSE 0; INK 1;" char";h$
7793 PRINT AT 14,1;">F"; INK 4;": Flip vert.";AT 14,16;i$;"F"; INK 4;": Flip horiz.";AT 16,1;i$; INK 4;r$;": Rot.Clockw. ";i$; INK 4;r$;": Rot.AClkw.": POKE VAL "22978",79: POKE VAL "22993",121: POKE VAL "23042",79: POKE VAL "23058",121
7795 PRINT AT 18,1;i$; INK 4;"5,6,7,8"; INK 6;"+"; PAPER 7; INK 2;"SShift"; PAPER 0; INK 4;": Displace "; INK 1;"chr";h$: FOR z=0 TO 7 STEP 2: POKE 23106+z,122: NEXT z: RETURN
7799 REM \: HelpPage4_SUBR
7800 PRINT AT 12,1; BRIGHT 0; INK 7;n$+"_"; INVERSE 1;"Clipboard"; INVERSE 0;"_0"; OVER 1;CHR$ 8+"/"; PAPER 0; INK 7;n$: PLOT 174,73
7805 PRINT AT 14,1;i$; INK 5;"CVal"; INK 4;": Copy "; PAPER 6; INK 0;"selected"; PAPER 0; INK 5;" value";h$;AT 16,1;i$; INK 1;"Char"; INK 4;": Copy "; INK 1;"character";h$
7807 PRINT AT 4,4;"Sorry, this Help system";AT 6,7;"is incomplete yet.";AT 8,4;"See the manual txt file";AT 10,3;"for a complete keys info."
7810 PRINT AT 18,1;i$; INK 4;"VPaste: Paste "; INK 1;"char"; INK 4;" or "; INK 5;"val";h$;AT 20,1;i$; INK 4; BRIGHT 0;"XClear"; BRIGHT 1;": Clear "; INK 6;"clipboard";h$
7815 FOR z=22658+320 TO 23170 STEP 64: POKE z+(z<22724)*2,121-(z=22850 OR z=22978)*42: LET z=z+(64 AND z=22850): NEXT z: RETURN
9000 PAPER 7: BORDER 7: INK 0: BRIGHT 0: CLS
9005 POKE 65368,149: POKE 65369,93: POKE 65370,127: POKE 65371,51: POKE 65372,173: POKE 65373,239: POKE 65374,127: POKE 65375,62
9010 LET b$=CHR$ 16+CHR$ 2+CHR$ 17+CHR$ 0+"A"+CHR$ 17+CHR$ 6+"B"+CHR$ 16+CHR$ 4+"A"+CHR$ 17+CHR$ 5+"B"+CHR$ 16+CHR$ 0+"A"
9015 LET di=23448: LET z=INT (PEEK 23611/16): LET w=z/2<>INT (z/2): LET w=(48 AND NOT w)+(128 AND w)
9020 IF PEEK 2899=165 THEN GO TO 9085
9030 IF PEEK 2899=159 THEN GO TO 9080
9040 IF PEEK 2899=126 THEN GO TO 9065
9050 LET md=0: LET w$="Unknown": RETURN
9060 LET md=3: LET w$="+3": RETURN
9065 LET di=23420: LET md=2: LET w$="+2A/B": IF PEEK 23398=148 OR PEEK 23398=180 OR PEEK 23417=65 OR PEEK 23417=67 OR PEEK 23418=65 OR PEEK 23418=67 THEN GO TO 9060
9070 RETURN
9080 LET md=1: LET w$="128/+2": RETURN
9085 LET md=0: LET w$="48k": LET di=23400: GO SUB 300: RETURN
9990 REM : CLOSE #
