Check 8A25CDD0
Auto 8224
   1 CLEAR VAL "62967"
   2 GO SUB VAL "460": GO SUB VAL "465": LET z=VAL "63039": LET z=PEEK z+PEEK (z+VAL "5")+PEEK (z+VAL "10")+PEEK (z+VAL "15"): IF z<>VAL "132" THEN LOAD "d_chars" CODE
   3 REM : USR 25200
   4 REM : LEN h$\#19950,i$\#19924,m$\#19925,k$\#19927,n$\#19924,v$\#1995
   5 REM : INT+ b,c,d,r,z,n,bi,md,a,p,pa,ro,u,h,zz,di,ex,ox,oy,dr,br,ol,ed,ar,sb,tn,tp,ag,ic,sv,ee
   6 REM : INT+ FN e,f
   7 REM : FN(INT+ z)
   9 REM : OPEN #
  10 GO TO 9e3
  11 DEF FN e(h$,z)=( CODE h$(z)-48-7*(h$(z)>"9"))*16+( CODE h$(z+1)-48-7*(h$(z+1)>"9"))
  12 DEF FN h$(z)=CHR$ (INT (z/16)+48+7*(INT (z/16)>9))+CHR$ (z-(INT (z/16)*16)+48+7*(z-(INT (z/16)*16)>9))
  15 PRINT #1;AT 0,0; PAPER 0;TAB 27;" ";AT 1,0;TAB 26: RETURN
  17 POKE 23658,0
  18 POKE 23560,0: PAUSE n: LET c=PEEK 23560: RETURN
  20 PRINT AT 0,7; PAPER 8; INK 9;i$: RETURN
  30 FOR z=32 TO 127: PRINT CHR$ z;: NEXT z: RETURN
  35 LET b=0: FOR z=0 TO 7: LET b=b+PEEK (d+z): NEXT z: RETURN
  36 LET di=d+p-24: LET z=INT (di/256): POKE 62984,di-(z*256): POKE 62985,z: LET di=USR 62975
  37 LET z=di+24: LET di=(65368 AND u=1)+(di-40 AND u<>1): GO SUB 60: PRINT AT 17,1; BRIGHT 0; INK 7;(CHR$ 16+CHR$ 3+"\  \  " AND u=1)+"AB\  EF\  IJ\  MN\  QR\  ";"UV" AND u<>1;AT 18,1;"\  \  " AND u=1;"CD\  GH\  KL\  OP\  ST\  ";"WX" AND u<>1: GO SUB 69
  38 LET di=79: LET z=(INT ((z-65368)/32)*3+2 AND u=1)+(6 AND u<>1): IF u=1 AND z>15 THEN RETURN
  39 POKE VAL "23073"+z,di: POKE VAL "23074"+z,di: POKE VAL "23105"+z,di: POKE VAL "23106"+z,di: RETURN
  40 IF c= CODE "Z" AND ar AND PEEK (d+p)>0 THEN GO TO 325
  41 IF c= CODE "D" AND ar THEN GO TO 7190
  42 IF c= CODE "+" AND NOT bi THEN GO TO 2235
  43 IF c= CODE "G" AND NOT bi THEN GO TO 2240
  44 IF c= CODE "g" AND NOT bi THEN GO SUB 2220
  48 IF (c= CODE "&" OR c= CODE "'") AND ar THEN GO SUB 230
  49 IF (c= CODE "(" OR c= CODE ")") AND ar THEN GO SUB 240
  50 IF (c= CODE "f" OR c= CODE "F") AND ar THEN GO SUB 255
  51 IF ((c= CODE "i" AND ed) OR (c= CODE "n" AND NOT ed) OR c= CODE "_") AND ar THEN GO SUB 250
  52 IF c= CODE "Q" THEN GO TO 810
  53 IF ed THEN GO TO 95
  54 GO TO 610
  55 LET sb=NOT sb: GO SUB 58: POKE VAL "22885",121-(34 AND sb): IF NOT sb THEN GO SUB 85
  57 RETURN
  58 BEEP .009,-11: RETURN
  59 BEEP .0003,34: RETURN
  60 POKE 63032,PEEK 23606: POKE 63033,PEEK 23607: LET di=di-520: LET r=INT (di/256): POKE 23606,di-(r*256): POKE 23607,r: RETURN
  61 POKE 23675,144: POKE 23676,245: RETURN
  62 POKE 23675,88: POKE 23676,255: RETURN
  63 RANDOMIZE USR 63039: RETURN
  65 FOR z=3 TO 20: PRINT AT z,1;TAB ((18-z AND z>18)+31): NEXT z: RETURN
  69 POKE 23606,PEEK 63032: POKE 23607,PEEK 63033: RETURN
  74 PRINT INK 4;AT 9,5;"Rot";AT 11,1;"Inv Bkg";AT 15,2;h$+"  ";AT 16,2;h$+"  ";AT 18,1,h$;AT 19,1,h$: POKE VAL "22821",78: POKE VAL "22881",79: POKE VAL "22885",121-(34 AND sb): RETURN
  75 GO SUB 58: LET ag=NOT ag: POKE VAL "22945",121-(34 AND ag): IF NOT ag THEN GO TO 77
  76 GO TO 36
  77 PRINT AT 17,1,"  ";AT 18,1,"  ": RETURN
  78 PRINT #1;AT 0,0; BRIGHT 1; PAPER 0; INK 4;"Exit("; INK 6;"no save"; INK 4;")";TAB 23;"Quit";AT 1,0; PAPER 1; INK 7;"Enter"; PAPER 0; INK 4;"("; INK 6;"save"; INK 4;" & "; INK 2;"exit"; INK 4;")       ": POKE VAL "23232",121: POKE VAL "23255",121: RETURN
  79 PRINT INK 4;AT 13,1;"Spr": POKE VAL "22945",121-(34 AND ag): RETURN
  80 IF ed THEN RETURN
  82 FOR z=0 TO 7: POKE z+63024,PEEK (z+d): NEXT z: RETURN
  84 PRINT AT 16,11; INK 5;"XDrw "; INK 2;"0Del": POKE VAL "23051",79+(8 AND dr): POKE VAL "23056",121-(34 AND br): RETURN
  85 PRINT AT 7,4; OVER 1; BRIGHT 8; INK ic;" ": IF sb THEN RETURN
  86 PRINT BRIGHT 0;AT 6,3;"\ .\..\. ";AT 7,3;"\ :";AT 7,5;"\: ";AT 8,3;"\ '\''\' ": RETURN
  87 GO SUB 61
  88 LET n$=CHR$ 16+(CHR$ tn+"." AND NOT bi)+(CHR$ 4+"0" AND bi): LET m$=CHR$ 16+(CHR$ tp+"\q" AND NOT bi)+(CHR$ 7+"1" AND bi): RETURN
  90 GO SUB 80: GO SUB 8990: POKE 63035,PEEK 23624: BEEP .04 AND NOT ed,12.9: LET h$="    ": LET b=22528: POKE b,69+9*((PEEK b)=77): GO SUB 85: GO SUB 588: POKE VAL "22640",7: LET dr=0: LET br=0: IF ed THEN GO TO 92
  91 GO SUB 74: LET ed=1: LET ex=3: IF c> CODE "0" AND c< CODE "9" THEN LET ex=c-49
  92 GO SUB 78: LET oy=p: LET ox=ex: GO SUB 84: PRINT AT 5,13; INK 6;"d";AT 5,17; INK 5;"V";AT 0,7; INK 6; PAPER 8;h$+" >Editing...   "+h$;AT 16,9;" ";AT 7+p,11; INK 5; BRIGHT 8; OVER 1;h$+" ";AT 7+p,17;"   ": GO SUB 79
  93 IF ag THEN GO SUB 36
  94 GO SUB 315: GO SUB 87: BORDER 5
  95 IF sb THEN GO SUB 300
  96 LET n=0: GO SUB 17: IF c= CODE "a" OR c= CODE "o" OR c= CODE "6" OR c=8 THEN GO TO 125
  97 IF (c= CODE "n" OR c=32 OR c= CODE "0" OR c= CODE "m") AND ar THEN GO TO 163
  98 IF c= CODE "x" AND ar THEN GO TO 220
  99 IF (c=12 OR (c= CODE "0" AND br)) AND ar THEN GO TO 225
 100 IF c= CODE "z" THEN GO TO 155
 101 IF c= CODE "d" OR c= CODE "p" OR c= CODE "7" OR c=9 THEN GO TO 130
 102 IF c= CODE "e" THEN GO TO 145
 103 IF c= CODE "w" OR c= CODE "9" OR c=11 THEN GO TO 135
 104 IF c= CODE "q" THEN GO TO 150
 105 IF c= CODE "s" OR c= CODE "8" OR c=10 THEN GO TO 140
 106 IF c= CODE "c" THEN GO TO 160
 107 IF (c= CODE "r" OR c= CODE "R") AND ar THEN GO SUB 280
 108 IF c= CODE "O" OR c= CODE "P" THEN GO TO 319
 109 IF c= CODE "S" THEN GO SUB 75
 110 IF c= CODE "b" THEN GO TO 318
 112 IF c=13 THEN GO TO 201
 113 IF c= CODE "E" THEN GO TO 203
 114 IF c= CODE "B" THEN GO SUB 55
 116 IF c= CODE "l" THEN GO SUB 1080
 117 IF c= CODE "<" THEN GO TO 124
 120 GO TO 40
 124 GO SUB 59: GO SUB 247: GO TO 40
 125 LET ex=ex-(ex>0): GO TO 210
 130 LET ex=(ex<7)+ex: GO TO 210
 135 LET p=p-(p>0): GO TO 210
 140 LET p=(p<7)+p: GO TO 210
 145 LET ex=(ex<7)+ex: GO TO 135
 150 LET ex=ex-(ex>0): GO TO 135
 155 LET ex=ex-(ex>0): GO TO 140
 160 LET ex=(ex<7)+ex: GO TO 140
 163 IF c= CODE "0" THEN LET c=32
 165 IF (br OR dr) AND c=32 THEN GO TO 170
 166 LET n=(POINT (ex+32,119-p)*(c=32))+(c= CODE "m"): BEEP .03,12.9-n*11
 167 PRINT AT p+7,ex+21; PAPER pa AND NOT bi; INK tp; BRIGHT 8;m$ AND NOT n;n$ AND n: PLOT INK 7; INVERSE n;ex+32,119-p: LET ic=7: GO SUB 315: LET c=PEEK (p*256+16612): POKE d+p,c: PRINT AT p+7,17; BRIGHT 8; INK 5;("$"+FN h$(c) AND h);STR$ c+"  "(LEN STR$ c TO ) AND NOT h: GO TO 95
 169 GO SUB 316: GO SUB 315: RETURN
 170 BEEP .03,(3 AND dr)+(10 AND br): LET br=0: LET dr=0: GO SUB 84: GO SUB 169: GO TO 95
 201 IF n THEN GO TO 205
 202 LET ee=1: GO TO 205
 203 FOR z=0 TO 7: POKE z+d,PEEK (z+63024): NEXT z: LET ee=0
 205 GO SUB 62: LET ic=3: LET sb=0: LET ed=0: POKE VAL "23232",87: BEEP .05,5-(c= CODE "E")*10: GO SUB 20: IF ag THEN GO SUB 77
 207 GO SUB 1310: GO SUB 2280: LET b=PEEK 63035: BORDER b=79: POKE 23624,b: LET b=22528: POKE b,5+72*((PEEK b)=78): INK 2: GO SUB 8990: LET u=2: GO TO 9190
 210 IF ex=ox AND p=oy THEN GO TO 95
 215 GO SUB 59: GO SUB 169: LET ox=ex: LET oy=p: IF dr THEN GO TO 222
 216 IF br THEN GO TO 227
 219 GO TO 95
 220 LET dr=NOT dr: LET br=0: GO SUB 84: BEEP .03,3+dr*7: PRINT AT 16,12; OVER 1; INK 5; FLASH dr;"   ": GO SUB 169
 222 LET n=0: GO TO 167
 225 LET br=NOT br: LET dr=0: GO SUB 84: BEEP .03,10-br*7: PRINT AT 16,17; OVER 1; INK 2; FLASH br;"   ": GO SUB 169
 227 LET n=1: GO TO 167
 230 GO SUB 35: IF NOT b THEN RETURN
 232 GO SUB 59: FOR z=0 TO 7: LET b=PEEK (z+d): POKE z+d,(((b*2) AND b<128)+((b-128)*2+1 AND b>127) AND c= CODE "&")+(INT (b/2)+(128 AND b/2<>INT (b/2)) AND c= CODE "'"): NEXT z: GO TO 246
 240 GO SUB 35: IF NOT b THEN RETURN
 243 GO SUB 59: IF c= CODE "(" THEN GO TO 245
 244 LET b=PEEK d: FOR z=1 TO 7: POKE z+d-1,PEEK (z+d): NEXT z: POKE d+7,b: GO TO 246
 245 LET b=PEEK (d+7): FOR z=0 TO 6: POKE 7-z+d,PEEK (6-z+d): NEXT z: POKE d,b
 246 IF ed THEN LET ic=7
 247 LET u=2: GO SUB 1030: IF ed THEN GO SUB 249
 248 GO SUB 588: RETURN
 249 GO SUB 87: GO SUB 315: RETURN
 250 GO SUB 58: FOR z=0 TO 7: POKE z+d,255-PEEK (z+d): NEXT z: GO TO 246
 255 GO SUB 35: IF NOT b OR b=2040 THEN RETURN
 256 GO SUB 58: IF c= CODE "f" THEN GO TO 260
 257 IF c= CODE "F" THEN GO TO 270
 260 POKE 23295,0: FOR z=4 TO 7: POKE z*256+20735,PEEK (d+7-z): NEXT z: FOR z=0 TO 3: POKE z+d,PEEK (d+7-z): NEXT z: FOR z=4 TO 7: POKE z+d,PEEK (z*256+20735): NEXT z: GO TO 246
 270 FOR n=0 TO 7: LET di=0: LET k=PEEK (d+n): FOR z=0 TO 7: LET k=INT k/2: LET di=di*2+(k<>INT k): NEXT z: POKE d+n,di: NEXT n: GO TO 246
 280 GO SUB 35: IF NOT b OR b=2040 THEN RETURN
 283 GO SUB 58: PRINT AT 7,6;" ": IF c= CODE "t" OR c= CODE "T" THEN LET c=c-2
 285 FOR n=0 TO 7: FOR z=0 TO 7: IF POINT (n+32,(-z+119 AND c= CODE "R")+(z+112 AND c= CODE "r")) THEN PLOT INK 0;z+48,(-n+119 AND c= CODE "r")+(n+112 AND c= CODE "R")
 290 NEXT z: NEXT n: FOR z=0 TO 7: POKE z+d,PEEK (z*256+16614): NEXT z: GO TO 246
 300 LET di=d: GO SUB 60: FOR z=0 TO 2: PRINT INK ic;AT 6+z,3; PAPER ic=7;"AAA": NEXT z: GO SUB 69: RETURN
 315 LET ol=ATTR (p+7,ex+21): POKE p*32+ex+22773,207-(ol<64)*64+(br*8)+(dr*32)-dr*4: RETURN
 316 POKE oy*32+ox+22773,ol: RETURN
 318 GO SUB 316: GO SUB 7370: GO SUB 87: GO SUB 315: GO TO 95
 319 LET n=d: LET d=d+(8 AND c= CODE "P")-(8 AND c= CODE "O"): IF d<32768 THEN GO TO 324
 321 GO SUB 58: LET u=3-(d<65360): LET ic=3: GO SUB 1030: GO SUB 87: GO SUB 315: GO SUB 82: GO TO 95
 324 LET d=n: GO TO 95
 325 BEEP 0.069,12.9: LET b=0: GO TO 7235
 330 LET di=n-h: LET b=di: LET v$="": PRINT #1; PAPER 0;TAB 26: PRINT #1; PAPER 0; INK 5+(di>3);AT 1,1;"$" AND h;
 335 PRINT #1; PAPER 1; INK 0; FLASH 1;"\::";
 340 POKE 23658,8: LET n=1500: GO SUB 18: POKE 23658,0: IF NOT c THEN GO TO 395
 341 IF c=12 THEN GO TO 400
 343 IF c=13 AND LEN v$ THEN GO TO 362
 345 IF NOT h AND (c<48 OR c>57) THEN GO TO 395
 350 IF h AND (c<48 OR c>57) AND (c<65 OR c>70) THEN GO TO 395
 355 GO SUB 59: PRINT #1; PAPER 0; INK 5+(di>3);CHR$ 8+CHR$ c;: IF c=0 THEN GO TO 395
 360 LET v$=v$+CHR$ c: LET b=b-1: IF b THEN GO TO 335
 362 IF c=13 THEN GO SUB 59
 363 LET v$=(("0000" AND NOT h)+("0" AND di<3 AND h)+("000" AND di>2 AND h))(LEN v$ TO )+v$: IF NOT h THEN GO TO 375
 366 LET b=FN e(v$,1): IF di>3 THEN LET b=FN e(v$,3)+(256*b)
 370 RETURN
 375 LET di=LEN v$: IF v$>"65535" THEN GO TO 395
 380 LET b=(( CODE v$(1)-48)*1e4 AND di=5)+(( CODE v$(2)-48)*1e3 AND di>3)+(( CODE v$(3)-48)*100 AND di>2)+(( CODE v$(4)-48)*10 AND di>1)+( CODE v$(5)-48 AND di): RETURN
 395 LET b=d: IF PEEK 63038=100 THEN LET b=0
 397 RETURN
 400 IF b=di THEN GO TO 340
 405 GO SUB 59: LET b=b+1: PRINT #1; PAPER 0;CHR$ 8+CHR$ 8+"  "+CHR$ 8+CHR$ 8;: LET v$=v$( TO LEN v$-1): GO TO 335
 410 IF d+p<32768 THEN GO TO 610
 413 POKE 62989,INT ((d+p)/256): POKE 62988,d+p-(256*PEEK 62989): GO SUB 450: IF b>=r THEN GO SUB 460
 420 GO TO 470
 430 IF d+p<32768 THEN GO TO 610
 433 POKE 62991,INT ((d+p)/256): POKE 62990,d+p-(256*PEEK 62991): GO SUB 450: IF r<=b THEN GO SUB 465
 437 GO TO 470
 440 IF NOT b THEN GO TO 445
 443 LET n=INT (b/256): PRINT #1; PAPER 0; INK 2;AT 1,10;">"; INK 5;"$" AND h;CHR$ 16+CHR$ 6+CHR$ 17+CHR$ 0+(STR$ b AND NOT h)+(FN h$(n)+FN h$(b-(256*n)) AND h)
 445 IF NOT r THEN RETURN
 449 LET n=INT (r/256): PRINT #1; PAPER 0; INK 2;AT 1,17;"to "; INK 5;"$" AND h;CHR$ 16+CHR$ 6+CHR$ 17+CHR$ 0+(STR$ r AND NOT h)+(FN h$(n)+FN h$(r-(256*n)) AND h): RETURN
 450 LET b=PEEK 62988+(256*PEEK 62989): LET r=PEEK 62990+(256*PEEK 62991): RETURN
 460 LET r=0: POKE 62990,0: POKE 62991,0: PRINT #1; PAPER 0;AT 1,17;TAB 26: RETURN
 465 LET b=0: POKE 62988,0: POKE 62989,0: PRINT #1; PAPER 0;AT 1,10;TAB 16: RETURN
 470 GO SUB 440: GO SUB 58: GO TO 610
 480 GO SUB 450: IF NOT b AND NOT r THEN GO TO 610
 485 BEEP .09,13: GO SUB 460: GO SUB 465: GO TO 610
 530 LET d=d-768: GO TO 590
 531 LET d=d+768: GO TO 590
 533 LET d=d+64: GO TO 590
 536 LET d=d-64: GO TO 590
 540 LET d=d-32: GO TO 590
 545 LET d=d+32: GO TO 590
 550 LET d=d+1: GO TO 590
 555 LET d=d+4: GO TO 590
 560 LET d=d-1: GO TO 590
 565 LET d=d-4: GO TO 590
 570 LET d=d-8: GO TO 590
 575 LET d=d+8: GO TO 590
 580 LET z=p: LET p=p+(p<7)*1: IF p=z THEN LET p=0
 581 GO TO 585
 583 LET z=p: LET p=p-(p>0)*1: IF p=z THEN LET p=7
 585 GO SUB 59: PRINT AT z+7,11; BRIGHT 8; OVER 1; INK 5;"\::\::\::\::\::";AT z+7,17;"   ": GO SUB 820: GO SUB 588: IF u THEN GO SUB 760
 587 GO TO 610
 588 IF ed THEN RETURN
 589 PRINT OVER 1; BRIGHT 8; INK 6;AT p+7,11;"\::\::\::\::\::";AT p+7,17;"   ": RETURN
 590 GO SUB 59
 595 GO SUB 1030: GO SUB 588: LET ee=0: IF ed THEN GO TO 90
 610 LET n=0: GO SUB 17: IF c= CODE "o" OR c=8 THEN GO TO 570
 623 IF c= CODE "p" OR c=9 THEN GO TO 575
 625 IF c= CODE "s" OR c=10 THEN GO TO 580
 627 IF c= CODE "w" OR c=11 THEN GO TO 583
 631 IF c= CODE "K" THEN GO TO 531
 633 IF c= CODE "I" THEN GO TO 530
 635 IF c= CODE "O" THEN GO TO 540
 636 IF c= CODE "P" THEN GO TO 545
 639 IF c= CODE "i" THEN GO TO 536
 650 IF c= CODE "k" THEN GO TO 533
 653 IF c= CODE "q" THEN GO TO 565
 655 IF c= CODE "W" THEN GO TO 560
 660 IF c= CODE "a" THEN GO TO 555
 665 IF c= CODE "S" THEN GO TO 550
 670 IF c= CODE "h" THEN GO SUB 7600
 675 IF c= CODE "l" THEN GO TO 1430
 690 IF c= CODE "v" AND ar THEN GO TO 7220
 695 IF c= CODE "d" THEN GO TO 7250
 705 IF c= CODE "X" AND a<2 THEN GO TO 2150
 707 IF c= CODE "A" OR c= CODE "L" THEN GO TO 2500
 710 IF c= CODE "V" AND ar AND a<2 THEN GO TO 1980
 715 IF c= CODE "H" THEN GO TO 1300
 719 IF c= CODE "r" THEN GO TO 590
 721 IF c= CODE "b" THEN GO TO 742
 723 IF c= CODE "B" THEN GO TO 7450
 725 IF c= CODE "R" THEN GO TO 9e3
 726 IF c= CODE "/" OR c= CODE ":" OR c= CODE "\`" THEN GO TO 3500
 727 IF ((c> CODE "0" AND c< CODE "9") OR c= CODE "e" OR c=13) AND ar THEN GO TO 90
 728 IF c= CODE "." THEN GO TO 480
 729 IF c= CODE "m" THEN GO TO 410
 730 IF c= CODE "M" THEN GO TO 430
 731 IF c= CODE "c" THEN GO SUB 1690
 733 IF c= CODE "C" THEN GO SUB 1810
 737 IF (c= CODE "t" OR c= CODE "T") AND ar THEN GO SUB 280
 740 GO TO 40
 742 GO SUB 7370: GO SUB 588: GO TO 610
 760 LET b=d+p<65368: IF b AND u=2 THEN RETURN
 763 IF b THEN GO TO 780
 765 LET c=PEEK 63034: LET n=INT ((65535-(d+p))/8): IF c=n AND u=1 THEN RETURN
 769 IF u=1 THEN GO TO 775
 770 LET di=65368: GO SUB 60: LET m$="ABCDEFGHIJKLMNOPQRS": PRINT AT 20,6; INK 1;m$; BRIGHT NOT md;"TU"
 771 GO SUB 69
 773 PRINT AT 20,1; INK 3;"UDG "; INK 2;">";AT 20,27; INK 5;"<";AT 21,6; BRIGHT 0; INK 1; INVERSE 1;m$; INVERSE NOT md;"TU"; INVERSE 0; INK 5;"<"; INK 2;AT 21,5;">": LET u=1: GO TO 779
 775 POKE 23194-c,1: POKE 23226-c,1
 779 POKE 63034,n: POKE 23194-n,71: POKE 23226-n,57: RETURN
 780 PRINT AT 20,1;TAB 28'AT 21,5;TAB 28
 785 PLOT 16,0: DRAW INK 2;207,0: LET u=0: RETURN
 810 GO SUB 63: GO SUB 62: BEEP .09,12.9: POKE 63038,0: POKE 23658,0: POKE VAL "23255",87: PRINT #1;AT 1,0; PAPER 0; INK 9; BRIGHT 0;">Use RUN to reload program": BRIGHT 0: PAPER 7: BORDER 7: INK 0: PAUSE 0: STOP
 820 LET z=d+p: LET n$="Screen": LET m$=("A1ROM" AND z<15616)+("B5In ROM CharSet" AND (z>15615 AND z<16384))+("C71/3 "+n$ AND (z>16383 AND z<18432))+("D72/3 "+n$ AND (z>18431 AND z<20480))+("E73/3 "+n$ AND (z>20479 AND z<22528))+("F7"+n$+" Colour Attrs." AND (z>22527 AND z<23296))+("G6System Vars. (128k)" AND ((z>23295 AND z<23734) AND md))+(("H6Printer Buffer (48k)" AND (z>23295 AND z<23552))+("I6System Vars. (48k)" AND (z>23551 AND z<23734)) AND NOT md)
 830 LET n$="MH CharSet ": LET m$=m$+("J2System (Basic & more)" AND (z>23733 AND z<25200))+("L2This program" AND ((z>25199 AND z<55400) OR (z>62967 AND z<63039)))+("M4Free User Memory" AND (z>55399 AND z<62968))+("N2"+n$+"routine" AND (z>63038 AND z<63064))+("O4"+n$+"1" AND (z>63063 AND z<63832))+("P4"+n$+"2" AND (z>63831 AND z<64600))+("Q4"+n$+"3" AND (z>64599 AND z<65368))+("R3UDG" AND (z>65367 AND z<65520))+("S3UDG (48k)" AND z>65519)
 920 IF c= CODE "w" OR c= CODE "s" THEN GO TO 1005
1000 LET di=d-120: GO SUB 60: PRINT AT 3,1; BRIGHT 0; INK 2;"ABCDEFG"; INK 6;"HIJKLMNO"; INK 4;"PQRSTUVW"; INK 5;"XYZ[\\]^": POKE VAL "22640",71
1005 GO SUB 69: IF p(11)= CODE m$(1) THEN RETURN
1010 LET p(11)= CODE m$(1): IF p(11)= CODE "R" OR p(11)= CODE "S" THEN LET u=2
1015 LET n= CODE m$(2)-48: LET m$=m$(3 TO ): LET m$=m$+" area": LET m$=m$+(" !" AND n=6): LET ar=(n=3 OR n=4 OR n=6): IF NOT ar THEN LET m$=m$+"  "
1025 PRINT BRIGHT 0; PAPER n; INK 9;AT 2,1;TAB 31'AT 2,INT ((33-LEN m$)/2);m$;: IF  CODE m$(LEN m$)>32 THEN RETURN
1027 LET di=63e3: GO SUB 60: PRINT BRIGHT 0; PAPER n; INK 9;CHR$ 8;"A": GO SUB 69: RETURN
1030 GO SUB 820: LET di=d: GO SUB 60: PRINT AT 7,4; INK ic;"A" AND NOT sb: GO SUB 69: IF u THEN GO SUB 760
1035 IF ed AND ag THEN GO SUB 36
1040 IF ee THEN GO TO 316
1050 INK 5: GO SUB 61: FOR z=0 TO zz: LET b=d+z+(p AND NOT zz): LET n=PEEK b: LET c=INT (n/16): LET r=n-16*c: LET n$=b$(c+1)+b$(r+1): LET v$=STR$ n+"  ": LET m$="0000"+STR$ b AND NOT h: IF h THEN GO SUB 1250
1060 LET c=7+z+(p AND NOT zz): PRINT INK 7; BRIGHT 8;AT c,9;"*";AT c,21; PAPER pa AND NOT bi;n$;AT c,11;CHR$ 19+CHR$ 8+(m$(LEN STR$ b TO ) AND NOT h)+(m$ AND h);AT c,17;v$( TO 3): LET m$=(CHR$ 1+"." AND n<32)+(CHR$ 4+CHR$ n AND (n>31 AND n<144))+(CHR$ 6+"T" AND ((md AND n>163) OR n>164)): IF m$="" AND (n>143 AND n<165) THEN LET m$=CHR$ 3+CHR$ (n-79)
1070 PRINT AT c,9; BRIGHT 8;CHR$ 16+m$: NEXT z: GO SUB 62: LET zz=7: RETURN
1080 GO SUB 59: GO SUB 820: LET u=0: GO SUB 760: GO SUB 88: IF ag THEN GO SUB 36
1090 RETURN
1250 LET v$="$"+FN h$(PEEK b): LET r=INT (b/256): LET m$="$"+FN h$(r)+FN h$(b-(256*r)): RETURN
1300 LET h=NOT h: POKE VAL "22945",121-(34 AND h): GO SUB 2285: GO SUB 1310: GO TO 590
1310 LET ro=1: IF NOT a THEN GO SUB 1690
1315 IF a=1 THEN GO SUB 1810
1320 LET ro=0: RETURN
1430 GO SUB 58: PRINT #1; BRIGHT 1; PAPER 0; INK 6;AT 0,0;"+"; PAPER 7; INK 1;"CShift"; PAPER 0; INK 4;":To end   ";AT 1,0;"0-9:Others";TAB 26: PRINT AT 0,7; INK 7; BRIGHT 8; PAPER 8;" >Select location:     ": POKE VAL "23250",87: POKE VAL "23264",79: POKE VAL "23266",79: LET n$=("48k" AND NOT md)+("128k" AND md)+CHR$ 16+CHR$ 0+")  "+(" " AND NOT md): LET m$="MH Charset "
1460 PRINT AT 5,9; BRIGHT 0; INK 9; PAPER 7;"U-"; PAPER 3;"UDG"; PAPER 7;" ("; INK 2;n$;AT 7,9; INK 9; PAPER 7; BRIGHT 1;"R-"; PAPER 5;"ROM";m$(3 TO ); PAPER 7;AT 9,9; BRIGHT 0;"A-"; PAPER 4;m$;"1";AT 11,9; PAPER 7; BRIGHT 1;"B-"; PAPER 4;m$;"2";AT 13,9; PAPER 7; BRIGHT 0;"C-"; PAPER 4;m$;"3"
1490 PRINT BRIGHT 0; PAPER 7;AT 6,17; INK 1;("65512" AND md)+("65528" AND NOT md): PRINT BRIGHT 0;AT 6,11; INK 1; PAPER 7;"65368 "; BRIGHT 1;AT 8,11;"15616 16376"; BRIGHT 0;AT 10,11;"63064 63824"; BRIGHT 1;AT 12,11;"63832 64592"; BRIGHT 0;AT 14,11;"64600 65360": LET r=0
1495 FOR z=6 TO 14 STEP 2: PRINT BRIGHT r; PAPER 7;AT z,9; INK 9;" (";AT z,16;"-";AT z,22;")"; OVER 1; BRIGHT 0; INK 1; PAPER 0;AT z,23;"\::";AT z+1,23;"\::": LET r=NOT r: POKE 22697+(z-6)*32,78: NEXT z: PRINT OVER 1; BRIGHT 0; PAPER 1; INK 0;AT 15,10;TAB 23: LET n=1500: GO SUB 17: LET b=d: IF c= CODE "Q" THEN GO TO 810
1547 LET d=((65528 AND NOT md)+(65512 AND md) AND c= CODE "U")+(65368 AND c= CODE "u")+(15616 AND c= CODE "r")+(16376 AND c= CODE "R")+(63064 AND c= CODE "a")+(63824 AND c= CODE "A")+(63832 AND c= CODE "b")+(64592 AND c= CODE "B")+(64600 AND c= CODE "c")+(65360 AND c= CODE "C")+(16384 AND c= CODE "1")+(18432 AND c= CODE "2")+(20480 AND c= CODE "3")+(22528 AND c= CODE "4")+(23296 AND c= CODE "5")+(25200 AND c= CODE "6")+(55400 AND c= CODE "7")+(62968 AND c= CODE "8")+(63040 AND c= CODE "9")
1550 LET d=((d AND d)+(b AND NOT d))*(c<> CODE "0"): IF c THEN GO SUB 58
1650 FOR z=5 TO 15: PRINT AT z,9;TAB 24: NEXT z: GO SUB 2280: GO SUB 7495: GO TO 9150
1690 IF ro=1 THEN GO TO 1720
1710 LET a=0: LET p(1)=PEEK (d+p): LET b=d+p: LET p(9)=d+p: POKE VAL "23051",87: POKE VAL "23056",121
1720 LET v$=STR$ p(1)+"  ": LET m$="": GO SUB 1800: IF NOT h THEN LET m$="0000"+STR$ p(9)
1760 IF h THEN GO SUB 1250
1770 PRINT AT 18,2;"       0"+CHR$ 8; OVER 1;"/"; OVER 0;" ";m$(LEN STR$ p(9) TO ) AND NOT h;m$ AND h; INK 4;","; PAPER 6;(CHR$ 16+CHR$ 9+v$)( TO 5); PAPER 0;AT 19,8;CHR$ 23+CHR$ 20: PLOT INK 6;78,25: POKE 21575,p(1): POKE VAL "23111",5: IF ro THEN GO TO 1786
1785 LET b=PEEK 23624: BORDER 2: BEEP .069,12.9: BORDER 1*(b=79): POKE 23624,b
1786 LET c=0: RETURN
1800 PRINT INK 4;AT 15,2;"VPaste";AT 16,2; BRIGHT 0;"XClear  ": POKE VAL "23010",121: POKE VAL "23042",121-64: RETURN
1810 IF ro=1 THEN GO TO 1860
1820 LET a=1: POKE VAL "23051",79: POKE VAL "23056",87: LET p(9)=d: FOR z=0 TO 7: LET p(z+1)=PEEK (d+z): NEXT z: LET p(10)=d+7
1860 LET b=1: GO SUB 1930
1870 PRINT AT 18,4;"0"+CHR$ 8; OVER 1;"/"; OVER 0; INK 7;" from ";m$; INK 4; PAPER 0;",";(CHR$ 16+CHR$ 6+v$)( TO 5): PLOT INK 6;38,25: GO SUB 1800: LET b=8: GO SUB 1930: PRINT AT 19,8; INK 9;"to "; BRIGHT 0;m$; INK 4; PAPER 0;",";(CHR$ 16+CHR$ 6+v$)( TO 5): POKE VAL "23106",0: FOR z=0 TO 7: POKE z*256+20546,p(z+1): NEXT z: POKE VAL "23106",5: INK 5: IF ro THEN RETURN
1910 LET b=PEEK 23624: BORDER 2: BEEP .06,12.9: BORDER 4: BEEP .07,12.9: BORDER 1*(b=79): POKE 23624,b: RETURN
1930 LET v$=STR$ p(b)+"  ": IF NOT h THEN LET m$=("0000"+STR$ p((b=8)+9))(LEN STR$ p((b=8)+9) TO )
1950 LET b=p((b=8)+9): IF h THEN GO SUB 1250
1960 LET m$=CHR$ 16+CHR$ 0+CHR$ 17+CHR$ 6+m$: RETURN
1980 GO SUB 35: LET di=PEEK 23624: IF NOT b THEN GO TO 2065
2010 LET h$=" Overwrite  Char ": IF a=0 THEN GO TO 2065
2020 GO SUB 7180: IF c= CODE "Y" THEN GO TO 2065
2025 GO TO 610
2065 LET u=2: IF NOT a THEN GO TO 2110
2070 BEEP .07,14.9: FOR z=0 TO 7: POKE d+z,p(1+z): NEXT z: GO TO 2120
2110 POKE d+p,p(1)
2120 BEEP .08,14.9: BORDER 1*(di=79): POKE 23624,di: LET zz=a*7: IF a=1 THEN GO SUB 2280
2135 GO TO 590
2150 LET a=2: POKE VAL "23051",79: POKE VAL "23056",121: LET m$="    ": PRINT AT 15,2;m$+"  ";AT 18,2,m$;AT 19,8,m$;AT 16,2;m$+"   0"; OVER 1;CHR$ 8+"/": PLOT INK 6;78,41: LET b=PEEK 23624: BORDER 7: BEEP .16,12.9: BORDER 1*(b=79): POKE 23624,b: GO TO 610
2220 IF ed THEN GO SUB 316
2223 GO SUB 58: LET pa=pa+1: GO SUB 2232: IF pa=tp THEN LET pa=pa+1
2225 GO SUB 2232: FOR z=0 TO 7 STEP 2: PRINT INK 8; PAPER pa; OVER 1;AT 7+z,21;k$(4 TO );AT 8+z,21;k$( TO 24): NEXT z: IF ed THEN GO SUB 315
2231 RETURN
2232 IF pa>7 THEN LET pa=0
2233 RETURN
2235 GO SUB 58: LET tp=tp+1: IF tp=pa THEN LET tp=tp+1
2236 IF tp>7 THEN LET tp=pa=0
2237 GO SUB 7330: IF NOT ed THEN GO TO 595
2238 GO SUB 1030: GO SUB 87: GO SUB 315: GO TO 95
2240 GO SUB 58: LET tn=tn+1: IF tn>7 THEN LET tn=0
2245 GO TO 2237
2260 LET m$=CHR$ 16+CHR$ 2+CHR$ 17+CHR$ 0+"A"+CHR$ 16+CHR$ 6+CHR$ 17+CHR$ 2+"A"+CHR$ 16+CHR$ 4+CHR$ 17+CHR$ 6+"A"+CHR$ 16+CHR$ 5+CHR$ 17+CHR$ 4+"A"+CHR$ 16+CHR$ 0+CHR$ 17+CHR$ 5+"A": LET di=63008: GO SUB 60: PRINT #1; BRIGHT 1;AT 0,27;m$;AT 1,26;m$+"\::": GO SUB 69
2280 PRINT #1; INK 4; PAPER 0; BRIGHT 1;AT 0,0;"HBlk MHB"; INK 4;" Edit Rld Loc  Quit";AT 1,0;"SAve Load";TAB 26: POKE VAL "23232",79: POKE VAL "23239",121: POKE VAL "23241",79: POKE VAL "23246",78: POKE VAL "23250",79: POKE VAL "23255",121: POKE VAL "23265",121: POKE VAL "23269",121
2285 GO SUB 450: GO SUB 440: RETURN
2320 PAPER 0: BORDER 1: BRIGHT 1: CLS : PLOT 0,167: DRAW 8,0: DRAW INK 2;8,0: DRAW INK 6;8,0: DRAW INK 4;8,0: INK 5: DRAW 223,0: DRAW 0,-167: INK 2: DRAW -255,0: DRAW 0,166: POKE 23624,79: GO SUB 2260: GO SUB 20: PRINT AT 0,31; PAPER 2; INK 7;"X": LET di=63008: GO SUB 60: PRINT AT 0,1;m$: FOR z=1 TO 4: PRINT AT 22-z,z+27;m$( TO 25-5*z): NEXT z: PRINT AT 0,0; BRIGHT 0; INK 5;"B": GO SUB 69: RETURN
2500 LET sv=c= CODE "A": LET ex=0: LET zz=0
2501 GO SUB 3395: BEEP .09,13: LET m$=("Save" AND sv)+("Load" AND NOT sv)+" data menu": LET n=4: GO SUB 1025: LET m$=CHR$ 16+CHR$ 4+(": Save " AND sv)+(": Load " AND NOT sv): LET v$=("from " AND sv)+("to " AND NOT sv): LET n$=CHR$ 16+CHR$ 0+CHR$ 17+CHR$ 2+"M"+CHR$ 16+CHR$ 7+"H"+CHR$ 16+CHR$ 4+CHR$ 17+CHR$ 0: GO SUB 2790
2503 PRINT AT 4,1; INK 4;">1";m$; INK 3;"UDG"; INK 4;"'s.";AT 6,1;">2";m$;"all ";n$;" CharSets.";AT 7,5;"(R: "; INK 6;"With"; INK 4;"/Without routine)";AT 9,1;">3";m$;"CharSet ";v$;n$;" area.";AT 10,5;"(M: ";n$;" area 1/2/3)";AT 12,1;">4";m$;v$; INK 2;"Init " AND NOT sv;"Mark";"s" AND sv; INK 4;".": FOR z=0 TO 4 STEP 2: POKE 22865+z,70: NEXT z
2505 PRINT AT 14,1; INK 4;">5";m$;v$; INK 5;"custom ";"range" AND sv;"address" AND NOT sv; INK 4;"." AND NOT sv;AT 16,1;">6";m$;("from "+CHR$ 16+CHR$ 2+"file header" AND NOT sv); INK 4;".": IF sv THEN GO SUB 2850
2507 GO SUB 2840: GO SUB 2781
2510 FOR z=0 TO 384-(64 AND sv) STEP 64: POKE z+22658+(4 AND (z=96 OR z=192)),79
2511 IF z=64 OR z=160 THEN LET z=z-32
2512 NEXT z: GO SUB 450: IF NOT b OR (NOT r AND sv) THEN PRINT AT 12,1; OVER 1; INK 1; BRIGHT 0;TAB 25
2513 PRINT AT 20,3; INK 6;">"; INVERSE 1;"Other key to go back"; INVERSE 0;"<"
2515 POKE 63038,100: LET b=0: LET n=1e4: GO SUB 17: IF c= CODE "r" OR c= CODE "w" THEN GO TO 2740
2520 IF c= CODE "m" THEN GO TO 2783
2527 IF c= CODE "t" THEN GO TO 2750
2530 IF c= CODE "1" THEN GO TO 2570
2535 IF c= CODE "2" THEN GO TO 2580
2540 IF c= CODE "3" THEN GO TO 2590
2545 IF c= CODE "4" THEN GO TO 2610
2550 IF c= CODE "5" THEN GO TO 2650
2555 IF c= CODE "6" THEN GO TO 2690
2560 IF c= CODE "Q" THEN GO TO 810
2565 POKE 63038,0: LET zz=7: GO TO 7480
2570 LET b=65368: LET r=168: GO TO 2775
2580 LET b=63064-(25 AND ex): LET r=2304+(25 AND ex): GO TO 2775
2590 LET b=zz*768+63064: LET r=768: GO TO 2775
2610 GO SUB 450: IF NOT b OR (NOT r AND sv) THEN GO TO 2770
2615 GO SUB 2625
2620 LET r=(r-b+1) AND sv: GO TO 2775
2625 IF NOT sv THEN POKE 63038,103
2627 RETURN
2650 GO SUB 59: GO SUB 15: GO SUB 2625: PRINT #1;AT 0,0; PAPER 0; INK 6;">"; INK 4;"Start save" AND sv;"Load" AND NOT sv;" address?"; INK 5;" (hex)" AND h: LET n=5: GO SUB 330: IF b<55400 THEN GO TO 2501
2654 LET k=b: IF NOT sv THEN GO TO 2665
2655 GO SUB 15: PRINT #1;AT 0,0; PAPER 0; INK 6;">"; INK 4;"End save address?"; INK 5;" (hex)" AND h: LET n=5: GO SUB 330: IF b<55400 OR b<=k THEN GO TO 2501
2660 LET r=b-k
2665 LET b=k: GO TO 2775
2690 IF sv AND PEEK 63036=3 THEN GO TO 2770
2695 POKE 63038,101+(NOT sv): GO SUB 58: GO SUB 65: GO TO 2775
2740 GO SUB 2780: GO TO 2515
2750 IF NOT md THEN GO TO 2770
2753 GO SUB 2800: GO TO 2515
2770 BEEP .1,-13: GO TO 2515
2775 GO SUB 58: LET ox=PEEK 63038: LET n=INT (b/256): POKE 62984,b-n*256: POKE 62985,n: IF NOT sv AND ox<>100 THEN GO TO 2777
2776 LET n=INT (r/256): POKE 62986,r-n*256: POKE 62987,n
2777 POKE 63037,sv: GO SUB 15: GO SUB 65: PRINT AT 4,1;"SAVE" AND sv;"LOAD" AND NOT sv;" CODE " AND ox<>101;STR$ b AND (ox=100 OR ox=103);","+STR$ r AND ((sv AND ox=100) OR (NOT sv AND ox=100))'': STOP
2780 GO SUB 58: GO SUB 2781: LET ex=NOT ex
2781 PRINT AT 7,9; OVER 1; INK 6;"\::\::\::\::" AND ex;AT 7,14;"\::\::\::\::\::\::\::" AND NOT ex: RETURN
2783 GO SUB 2835: GO TO 2515
2790 GO SUB 450: PRINT #1; PAPER 0;AT 0,0; INK 4;">";m$(5 TO );"To "; INK 6;"Tape"; INK 4;"/"; INK 6;"Disk"; INK 4;"/"; INK 6;"RAM"; INK 4;" Quit";AT 0,0; INK 1; BRIGHT 0; OVER 1;AT 0,14;"    " AND md<3;AT 0,0;TAB 23 AND NOT md: POKE VAL "23255",121: IF md THEN POKE VAL "23238",79
2791 GO SUB 2830: PRINT #1;AT 1,0; PAPER 0; INK 4-(2 AND (NOT r AND sv));" " AND NOT r;("Mark"+("s" AND r)+" from " AND (b OR r));AT 1,0; INK 2;" No marks" AND (NOT b AND NOT r);AT 1,17;"only" AND (NOT r AND b AND sv);AT 1,0;"Only ending mark" AND (NOT b AND r): RETURN
2800 GO SUB 58: GO SUB 2830
2810 IF md<3 THEN POKE 63036,n+2
2820 IF md=3 THEN POKE 63036,n+1
2825 IF PEEK 63036>3 THEN POKE 63036,1
2830 LET n=PEEK 63036: PRINT #1;AT 0,9; PAPER 0; INK 6; OVER 1;"\::\::\::\::" AND n=1;AT 0,14;"\::\::\::\::" AND n=2;AT 0,19;"\::\::\::" AND n=3: IF sv THEN GO SUB 2845
2831 IF NOT md THEN RETURN
2832 FOR z=0 TO 1: POKE z+23417,(84 AND n=1)+(65 AND n=2)+(77 AND n=3): NEXT z: RETURN
2835 GO SUB 58: POKE zz*2+22865,70: LET zz=zz+1: IF zz>2 THEN LET zz=0
2840 POKE zz*2+22865,112: RETURN
2845 IF PEEK 63036=3 THEN GO TO 2860
2850 PRINT AT 16,1; INK 4;">6";m$; INK 2;"complete"; INK 4;" program.": POKE VAL "23042",79
2860 IF PEEK 63036=3 THEN PRINT AT 16,1; OVER 1; INK 1; BRIGHT 0;TAB 27
2870 RETURN
3395 GO SUB 58: GO SUB 65: GO SUB 7495: GO SUB 780: RETURN
3490 BEEP .09,-13: GO TO 610
3500 LET ex=c: GO SUB 450: LET n=0: LET ox=d+p: LET oy=r-b: IF NOT b OR NOT r THEN GO TO 610
3501 IF (ox=b OR ox<55400 OR ox+oy<55400) AND ex<> CODE ":" THEN GO TO 3490
3502 IF (ox+n>62967 AND ox+n<63064 AND ex<> CODE ":") OR (b+n>62967 AND b+n<63064 AND ex<> CODE "/") THEN GO TO 3490
3503 IF n=oy THEN GO TO 3505
3504 LET n=n+1: GO TO 3502
3505 LET di=PEEK 23624: LET h$=(" Move Large Data " AND ex= CODE "\`")+(" Copy Large Data " AND ex= CODE "/")+(" Erase Large Data" AND ex= CODE ":"): GO SUB 7180: IF c<> CODE "Y" THEN GO TO 610
3510 GO SUB 450: BEEP .09,3: PRINT #1; PAPER 0; INK 6;AT 0,5;"  "; FLASH 1;"Please wait..."; FLASH 0;TAB 26: PRINT #1; PAPER 0;AT 1,0;TAB 26: LET n=0: LET c=0: IF ex= CODE ":" THEN GO TO 3520
3517 IF b<=ox THEN GO TO 3550
3520 IF NOT c THEN GO TO 3525
3523 LET z=b: LET b=r: LET r=z
3525 FOR z=b TO r STEP (1 AND NOT c)+(-1 AND c): IF ex<> CODE ":" THEN POKE ox+n,PEEK z
3535 IF ex<> CODE "/" THEN POKE z,0
3540 LET n=n+(1 AND NOT c)-(1 AND c): NEXT z: GO TO 3695
3550 LET n=oy: LET c=1: GO TO 3520
3695 BEEP .09,13: BORDER 1 AND di=79: POKE 23624,di: GO SUB 2280: GO TO 595
7175 IF ed THEN GO TO 7177
7176 GO SUB 2280: RETURN
7177 GO SUB 78: RETURN
7180 BORDER 6: LET v$="     ": PRINT #1;AT 0,0; BRIGHT 1; PAPER 0;v$; PAPER 7; INK 2; FLASH 1;h$; FLASH 0; PAPER 0;v$;AT 1,0; PAPER 0;v$; INVERSE 1; FLASH 1; PAPER 7;"   Sure (Yes)?   "; FLASH 0; INVERSE 0; PAPER 0;"    ": POKE VAL "23278",121: BEEP .23,-11.4: LET n=400
7181 GO SUB 17: IF c= CODE "Y" THEN GO TO 7186
7183 IF NOT c THEN GO TO 7185
7184 BORDER 2: BEEP .09,-14
7185 BORDER (1 AND di=79)+(5 AND ed): POKE 23624,di: GO TO 7175
7186 BORDER 4: RETURN
7190 GO SUB 35: IF NOT b THEN GO TO 53
7191 POKE VAL "22689",87: LET di=PEEK 23624: LET h$="   Delete Char   ": GO SUB 7180: IF c= CODE "Y" THEN GO TO 7210
7193 POKE VAL "22689",121: GO TO 53
7210 FOR z=0 TO 7: POKE d+z,0: NEXT z: BEEP .07,14.9: BORDER (1 AND di=79)+(5 AND ed): POKE 23624,di: LET u=2: POKE VAL "22689",121: IF ed THEN GO TO 7216
7215 GO SUB 2280: GO TO 590
7216 GO SUB 78: GO SUB 246: GO TO 95
7220 POKE VAL "22705",87: PRINT AT p+7,17; INK 5; OVER 1; INVERSE 1; FLASH 1;"   ": PRINT #1;AT 0,0; PAPER 0; INK 6;">"; INK 4;"New "; INK 5;"Value"; INK 4;" of "; INK 6;d+p; INK 4;"? "; INK 5;"(hex) " AND h;"      " AND NOT h: LET n=3: GO SUB 330: IF b>255 THEN LET b=PEEK (d+p)
7230 POKE VAL "22705",79: IF b=PEEK (d+p) THEN GO TO 7240
7235 POKE d+p,b: LET zz=0: LET u=2: IF ed THEN GO TO 7237
7236 GO SUB 2260: GO TO 590
7237 GO SUB 246: GO TO 95
7240 GO SUB 2260: PRINT AT p+7,17; INVERSE 1; OVER 1; INK 6;"   ": GO TO 610
7250 POKE VAL "22701",87: PRINT INK 5; PAPER 0; FLASH 1; OVER 1;AT 7,11;"     ": PRINT #1;AT 0,0; PAPER 0; INK 6;">"; INK 4;"New "; INK 6;"Memory Address"; INK 4;"? "; INK 5;"(hex) " AND h;"      " AND NOT h: LET n=5: GO SUB 330: POKE VAL "22701",79: IF b=d THEN GO TO 7290
7280 LET d=b: GO SUB 2260: GO TO 590
7290 GO SUB 2260: PRINT INK 5+(p=0); PAPER 0; OVER 1;AT 7,11;"     ": GO TO 610
7330 GO SUB 88: IF NOT bi THEN GO TO 7340
7337 PRINT INK 1;AT 19,21;"8";AT 18,21;"2426";AT 17,21;"16318421"; INK 5;AT 16,21;"^^^^^^^^": FOR z=0 TO 2: PRINT AT z+16,21; INK 8; OVER 1;k$(4 TO ): NEXT z
7340 LET b$(1)=n$+n$+n$+n$: LET b$(2)=n$+n$+n$+m$: LET b$(3)=n$+n$+m$+n$: LET b$(4)=n$+n$+m$+m$: LET b$(5)=n$+m$+n$+n$: LET b$(6)=n$+m$+n$+m$: LET b$(7)=n$+m$+m$+n$: LET b$(8)=n$+m$+m$+m$: LET b$(9)=m$+n$+n$+n$: LET b$(10)=m$+n$+n$+m$: LET b$(11)=m$+n$+m$+n$: LET b$(12)=m$+n$+m$+m$: LET b$(13)=m$+m$+n$+n$: LET b$(14)=m$+m$+n$+m$: LET b$(15)=m$+m$+m$+n$: LET b$(16)=m$+m$+m$+m$: RETURN
7370 LET bi=NOT bi: POKE VAL "22710",bi*8+79: GO SUB 59: IF NOT bi THEN GO TO 7390
7380 GO SUB 7330: GO TO 7395
7390 LET h$="    ": GO SUB 9205: PRINT AT 17,21;h$+h$;AT 18,21;h$;AT 19,21;" ": GO SUB 7330
7395 GO SUB 1030: RETURN
7450 GO SUB 63: BRIGHT 0: PAPER 7: BORDER 7: INK 9: CLS : LET m$="CHARSET NUMBER ": LET n$=m$( TO 7): PRINT AT 0,7; PAPER 0;"MICRO"; PAPER 2;"HOBBY"; PAPER 6;AT 0,18;"CharSets"
7455 PRINT INVERSE 1; INK 0;AT 3,0; PAPER 9;"  >";m$;"1: USR "; PAPER 6;"63044  "'''''' PAPER 9;"  >";m$;"2: USR "; PAPER 6;"63049  "'''''' PAPER 9;"  >";m$;"3: USR "; PAPER 6;"63054  "'''''' PAPER 9;"  >";n$;" IN ROM #: USR "; PAPER 6;"63039  ": PRINT #1;"   SELECT ";n$;" (AnyKey4ROM)"
7460 FOR b=0 TO 12 STEP 6: PRINT AT 5+b,0;: RANDOMIZE USR (INT (b/6)*5+63044): GO SUB 30: NEXT b: GO SUB 63: FOR z=22642 TO 23218 STEP 192: POKE z,57: NEXT z: LET n=6e3: GO SUB 17: IF c>48 AND c<52 THEN RANDOMIZE USR ((c-49)*5+63044)
7480 GO SUB 2320: GO SUB 1310: IF bi THEN GO SUB 7330
7485 GO TO 9150
7495 PRINT OVER 1; INK 8;AT 3,0;">";AT 3,31;"<": RETURN
7600 BORDER 0: GO SUB 58: POKE VAL "22528",77: POKE VAL "22529",74: POKE VAL "22533",105: PRINT AT 0,6; OVER 1; PAPER 1; INK 7;TAB 31
7690 RETURN
8990 PLOT 84,123: DRAW 152,0: DRAW 0,-72: DRAW -152,0: DRAW BRIGHT 1;0,71: PLOT 164,52: DRAW BRIGHT 1;0,70: RETURN
9000 PAPER 1: BORDER 1: BRIGHT 0: CLS : BEEP .09,12.3: BORDER 3
9030 GO SUB 63: DIM b$(16,12): LET bi=0: LET k$="": LET tn=1: LET tp=3: GO SUB 7330: LET n$=CHR$ 19+CHR$ 1+" ": LET m$=CHR$ 19+CHR$ 0+" ": FOR z=0 TO 3: LET k$=k$+m$+n$: NEXT z: LET k$=k$+m$: LET n$="      ": LET i$="": LET z=128: LET md=0: LET m$="Unknown": IF INKEY$="0" OR PEEK 2899=165 THEN GO TO 9080
9040 IF PEEK 2899=159 THEN GO SUB 9077
9045 IF PEEK 2899=126 THEN GO SUB 9075
9050 LET c=INT (PEEK 23611/16): IF c/2=INT (c/2) THEN GO TO 9065
9055 IF md=2 AND (PEEK 23398=148 OR PEEK 23398=180 OR PEEK 23417=65 OR PEEK 23418=65 OR PEEK 23417=67 OR PEEK 23418=67) THEN LET md=3
9060 GO TO 9070
9065 LET md=0: LET z=48: LET i$=" "
9070 LET i$=i$+m$+" in "+STR$ z+"k mode "+STR$ md+" ": GO TO 9090
9075 LET md=2: LET m$="+2A/B/+3": RETURN
9077 LET md=1: LET m$=" 128/+2": RETURN
9080 LET md=0: LET i$=n$+"48k mode 0 "+n$
9090 LET n=PEEK 63036: IF NOT n THEN POKE 63036,(1 AND md<3)+(2 AND md=3)
9095 GO SUB 2832
9100 GO SUB 2320: LET d=65368: DIM p(11): LET p=0: LET bi=0: LET a=2: LET pa=0: LET h=0: LET zz=7: LET ro=0: LET u=0: LET ed=0: LET sb=0: LET ag=0: LET ic=3: LET ee=0: POKE 63038,0
9150 GO SUB 7495
9155 LET p(11)= CODE "Z": POKE 63034,69: PRINT AT 5,11; INK 6;"MADdr"; INK 4;","; INK 5;"Val"; INK 4;"  Binary";AT 5,1; INK 1;"Del "; INK 5;"Zro"; INK 4;AT 9,1;"Flp": LET c=1: FOR z=7 TO 14: PRINT AT z,9; BRIGHT c; INK 4;"       ,   ";AT z,21; OVER 1; INK pa; PAPER pa AND NOT bi;k$(4 TO ) AND c;k$( TO 24) AND NOT c: LET c=NOT c: NEXT z: INK 2
9185 GO SUB 8990: GO SUB 20: INK 7: PLOT 8,143: DRAW INK 2;56,0: DRAW INK 6;64,0: DRAW INK 4;64,0: DRAW INK 5;55,0: POKE VAL "22672",2: POKE 16784,255: POKE VAL "22710",bi*8+79: POKE VAL "22689",121: POKE VAL "22693",121: IF ed THEN GO SUB 74
9187 IF ed THEN GO TO 9193
9190 INK 1: GO SUB 85: PRINT INK 6;AT 5,13;"D"; INK 4;AT 11,1;"INv Mrk";AT 13,1;"Hex";AT 9,5;"RoT": POKE VAL "22701",79: POKE VAL "22705",79: POKE VAL "22882",79: POKE VAL "22885",114: POKE VAL "22945",121-(34 AND h): POKE VAL "22817",78: POKE VAL "22823",78: INK 5
9193 POKE VAL "22817",78: PRINT INK 4; PAPER 0;AT 16,11; INK 5;"CVal "; INK 1;"Char ";AT 16,9;"0"+CHR$ 21+CHR$ 1+CHR$ 8+"/" AND a=2: IF a=2 THEN PLOT INK 6;78,41
9196 POKE 23658,0: POKE VAL "23051",79+(8 AND a=0): POKE VAL "23056",121-(34 AND a=1): IF NOT bi THEN GO SUB 9205
9201 GO TO 595
9205 PRINT INK 4; PAPER 0;AT 16,21;"Grid InK": POKE VAL "23061",78: POKE VAL "23068",122: RETURN
9990 REM : CLOSE #
